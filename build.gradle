plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.cyclonedx.bom' version '1.7.2'
    id 'war'
}

group = 'epams'
version = '0.0.2-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    developmentOnly
    providedRuntime
    runtimeClasspath {
        extendsFrom providedRuntime
    }
}

repositories {
    mavenCentral()
}

dependencies {
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

    // webauthn
    implementation 'com.yubico:webauthn-server-core:2.5.2'

    // Session DBMS 관리
    compileOnly 'org.springframework.session:spring-session-jdbc:3.2.2'
    implementation 'org.springframework.session:spring-session-core'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'    
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'
    
    // org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3에 포함된 logback 취약점 조치
    // CVE-2023-6378, CVE-2023-6481 
    implementation 'ch.qos.logback:logback-classic:1.4.14'
    implementation 'ch.qos.logback:logback-core:1.4.14'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.3'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
    
    compileOnly 'org.projectlombok:lombok'    
    annotationProcessor 'org.projectlombok:lombok'
    
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.oracle.database.jdbc:ojdbc11'    
    runtimeOnly 'com.h2database:h2'
    
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'io.micrometer:micrometer-core'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'org.owasp.encoder:encoder:1.2.3'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.cyclonedxBom {
    includeConfigs = ["runtimeClasspath"]
    projectType = "application"
    schemaVersion = "1.5"
    destination = file("build/reports")
    outputName = "bom"
    outputFormat = "json"
    includeBomSerialNumber = false
    componentVersion = "2.0.0"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked"
}

tasks.named('bootJar') {
    enabled = false
}

tasks.named('bootWar') {
    enabled = true
}

springBoot {
    mainClass = 'epams.EPamsApplication'
}

bootRun {
    if (project.hasProperty('spring.profiles.active') && project.property('spring.profiles.active') == 'prod') {
        configurations {
            runtimeClasspath {
                exclude group: 'org.springframework.boot', module: 'spring-boot-devtools'
            }
        }
    }
}
